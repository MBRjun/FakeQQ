' Gambas class file

Public CQProcess As Process

Public Sub Form_Open()
    g.localpath = User.Home & "/.local/share/fakeqq"
    Exec ["mkdir", "-p", g.localpath] '创建本地文件夹
    G.Conf = New Settings(g.localpath & "/fakeqq.conf")
    g.uin = G.Conf["Account/UIN", 10001]
    g.password = G.Conf["Account/Password", ""]
    g.protocol = G.Conf["Account/Protocol", "iPad"]
    txtUIN.Text = g.uin
    txtPassword.Text = g.password
    cbProtocol.Text = g.protocol
    Me.Title = Application.title & " " & Application.version & " - 登录"
    frmLoad.Title = Application.title & " " & Application.version
    lblTitle.Text = Application.title
    If Exist(g.localpath & "/node_modules") = False Then 
        Message("oicq 未找到，点击确定进行安装。")
        btnLogin.Text = "正在安装依赖"
        btnLogin.Enabled = False
        Shell "npm i oicq@1" With ["PWD=" & g.localpath] Wait
        btnLogin.Text = "登录"
        btnLogin.Enabled = True
    Endif
    '加载
    
End

Private Sub LoginQQ()
    g.uin = txtUIN.Text
    g.password = txtPassword.Text
    g.protocol = cbProtocol.text
    '保存配置
    g.conf["Account/UIN"] = g.uin
    g.conf["Account/Password"] = g.password
    g.conf["Account/Protocol"] = g.protocol
    '生成配置文件
    Select Case g.protocol
        Case "iPad"
        File.Save(User.home & "/.oicq/config.js", Replace(g.ConfigDefault, "PROTOCOL", "5"))
        Case "macOS"
        File.Save(User.home & "/.oicq/config.js", Replace(g.ConfigDefault, "PROTOCOL", "4"))
        Case "安卓手机"
        File.Save(User.home & "/.oicq/config.js", Replace(g.ConfigDefault, "PROTOCOL", "1"))
        Case "安卓手表"
        File.Save(User.home & "/.oicq/config.js", Replace(g.ConfigDefault, "PROTOCOL", "2"))
        Case "安卓平板"
        File.Save(User.home & "/.oicq/config.js", Replace(g.ConfigDefault, "PROTOCOL", "3"))
    End Select
    Try Kill g.localpath & "/qrcode.png"
    If Not Exist(g.localpath & "/fakeqqdata") Then'创建配置文件夹
        Mkdir g.localpath & "/fakeqqdata"
        Mkdir g.localpath & "/fakeqqdata/group_images"
        Mkdir g.localpath & "/fakeqqdata/user_images"
        Mkdir g.localpath & "/fakeqqdata/cq_images"
    Endif
    g.OutputCacheEnabled = True
    System.Shell = "/bin/bash"
    CQProcess = Shell g.localpath & "/node_modules/oicq/bin/oicq " & g.uin For Input Output As "CQProc"
    btnLogin.Text = "登录中 ..."
    btnLogin.Enabled = False
    Dim QRCompleted As Boolean = False, PasswordCompleted As Boolean = False
    Do Until InStr(g.outputcache, "你已成功登录") <> False
        Wait 0.1
        If InStr(g.outputcache, "请输入密码") <> False And PasswordCompleted = False Then
            PasswordCompleted = True 
            Wait 0.1
            Write #CQProcess, g.password & "\n"
        Endif
        If InStr(g.outputcache, "请用手机QQ扫描二维码") <> False And QRCompleted = False Then 
            Exec ["xdg-open", user.home & "/.oicq/" & g.uin & "/qrcode.png"]
            Message.Info("登录需要验证码，请扫码验证。\n扫码完成后，点击“我已扫码”。", "我已扫码")
            QRCompleted = True
            Write #CQProcess, "\n"
        Endif
    Loop
    g.OutputCacheEnabled = False
    btnLogin.Text = "登录成功"
    Me.Close
    frmMain.show
    
End

Public Sub CQProc_Read()
  Dim sLine As String
  Read #Last, sLine, -256
  Print sLine
  If G.OutputCacheEnabled Then g.outputcache &= sLine
End


Public Sub btnLogin_Click()
    If txtUIN.Text = "" Then Return
    LoginQQ
End

Public Sub lblQQPassword_MouseDown()
    If txtPassword.Password Then 
        txtPassword.Password = False
    Else 
        txtPassword.Password = True
    Endif
End

